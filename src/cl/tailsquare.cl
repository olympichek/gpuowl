// Copyright (C) Mihai Preda and George Woltman

#include "base.cl"
#include "fftheight.cl"
#include "tailutil.cl"
#include "middle.cl"

// If not doing L2 stripes, process the lines in any order.
// If L2 striping, process lines output by fftMiddleIn.  fftMiddleIn outputs 16 * MIDDLE tailSquare lines.
u32 get_line_number(u32 base) {
  u32 g = get_group_id(0);
#if !SINGLE_KERNEL
#if L2_STRIPING
  if (base == 0) g = g + 1;
#else
  g = g + 1;
#endif
#endif
#if L2_STRIPING
  // Old, simple L2 striping code
  // return get_group_id(1) * WIDTH + base + g;

  // Process all lines from low half of base_lo stripe group.  One stripe group is stripe_group_size * 16 * MIDDLE lines.
  u32 base_lo = base;
  u32 stripe_group_size = L2_STRIPING;
  u32 half_size = (MIDDLE + 1) / 2;  // For base_lo, round odd middles up.
  u32 kernelsToExecute = half_size * stripe_group_size * 16;
  if (g < kernelsToExecute) return g / (stripe_group_size * 16) * WIDTH + base_lo + g % (stripe_group_size * 16);
  g -= kernelsToExecute;

  // Process lines from low half of base_hi stripe group.  One stripe group is stripe_group_size * 16 * MIDDLE lines.
  // The first line in the base_hi stripe group is not ready for processing (except for the last group).
  u32 base_hi = WIDTH - stripe_group_size * 16 - base_lo;
  if (base_hi != WIDTH / 2) base_hi++;   // Skip first line in base_hi (usually)
  half_size = MIDDLE / 2;                // For base_hi, round odd middles up.
  return g % half_size * WIDTH + base_hi + g / half_size;
#else
  return g;
#endif
}

#if FFT_FP64

// Handle the final squaring step on a pair of complex numbers.  Swap real and imaginary results for the inverse FFT.
// We used to conjugate the results, but swapping real and imaginary can save some negations in carry propagation.
void OVERLOAD onePairSq(T2* pa, T2* pb, T2 t_squared) {
  T2 a = *pa;
  T2 b = *pb;

//  X2conjb(a, b);
//  *pb = mul2(cmul(a, b));
//  *pa = csqa(a, cmul(csq(b), -t_squared));
//  X2_conjb(*pa, *pb);
//  *pa = SWAP_XY(*pa), *pb = SWAP_XY(*pb)

  // Less readable version of the above that saves one complex add by using FMA instructions
  X2conjb(a, b);
  T2 twoab = mul2(cmul(a, b));                          // 2ab
  *pa = csqa(a, cfma(csq(b), -t_squared, twoab));       // final a = a^2 + 2ab - (bt)^2
  (*pb).x = fma(-2.0, twoab.x, (*pa).x);                // final b = a^2 - 2ab - (bt)^2
  (*pb).y = fma(2.0, twoab.y, -(*pa).y);                // conjugate(final b)
  *pa = SWAP_XY(*pa), *pb = SWAP_XY(*pb);
}

void OVERLOAD pairSq(u32 N, T2 *u, T2 *v, T2 base_squared, bool special) {
  u32 me = get_local_id(0);

  for (i32 i = 0; i < NH / 4; ++i, base_squared = mul_t8(base_squared)) {
    if (special && i == 0 && me == 0) {
      u[i] = SWAP_XY(2 * foo(u[i]));
      v[i] = SWAP_XY(4 * csq(v[i]));
    } else {
      onePairSq(&u[i], &v[i], base_squared);
    }

    if (N == NH) {
      onePairSq(&u[i+NH/2], &v[i+NH/2], -base_squared);
    }

    T2 new_base_squared = mul_t4(base_squared);
    onePairSq(&u[i+NH/4], &v[i+NH/4], new_base_squared);

    if (N == NH) {
      onePairSq(&u[i+3*NH/4], &v[i+3*NH/4], -new_base_squared);
    }
  }
}

#if !SINGLE_KERNEL
// The kernel tailSquareZero handles the special cases in tailSquare, i.e. the lines 0 and H/2
// This kernel is launched with 2 workgroups (handling line 0, resp. H/2)
KERNEL(G_H) tailSquareZero(P(T2) out, CP(T2) in, Trig smallTrig) {
  local T2 lds[LDS_BYTES / sizeof(T2)];
  T2 u[NH];
  const u32 H = ND / SMALL_HEIGHT;

  // This kernel in executed in two workgroups.
  u32 which = get_group_id(0);
  assert(which < 2);

  u32 line = which ? (H/2) : 0;
  u32 me = get_local_id(0);

  dependentLaunch();       // Next kernel will be tailSquareFP64 which must dependentLaunchWait before reading data from fftMiddleInFP64
  dependentLaunchWait();   // Previous kernel was fftMiddleInFP64 that launched dependents before writing FP64 data

  readTailFusedLine(in, u, line, me);

#if FFT_VARIANT_H != 0
  T2 w;
#elif NH == 8
  T2 w = fancyTrig_N(H * me);
#else
  T2 w = slowTrig_N(H * me, ND / NH);
#endif

  T2 trig = slowTrig_N(line + me * H, ND / NH);

  fft_HEIGHT1(lds, u, smallTrig, w, 1, me);
  reverse(lds, u + NH/2, !which);
  pairSq(NH/2, u,   u + NH/2, trig, !which);
  reverse(lds, u + NH/2, !which);

  fft_HEIGHT1(lds, u, smallTrig, w, 1, me);
  writeTailFusedLine(u, out, transPos(line, MIDDLE, WIDTH), me);
}
#endif

#if SINGLE_WIDE

KERNEL(G_H) tailSquare(P(T2) out, CP(T2) in, u32 base, Trig smallTrig) {
  local T2 lds[LDS_BYTES / sizeof(T2)];
  const u32 H = ND / SMALL_HEIGHT;

  T2 u[NH], v[NH];

  u32 line1 = get_line_number(base);
  u32 line2 = line1 ? H - line1 : (H / 2);
  u32 memline1 = transPos(line1, MIDDLE, WIDTH);
  u32 memline2 = transPos(line2, MIDDLE, WIDTH);

  u32 me = get_local_id(0);

  dependentLaunchWait();   // Previous kernel was fftMiddleInFP64 that launched dependents before writing FP64 data

  readTailFusedLine(in, u, line1, me);
  readTailFusedLine(in, v, line2, me);

#if FFT_VARIANT_H != 0
  T2 w;
#elif NH == 8
  T2 w = fancyTrig_N(H * me);
#else
  T2 w = slowTrig_N(H * me, ND / NH);
#endif

  u32 zerohack = ZEROHACK_H * (u32) get_group_id(0) / 131072;
  fft_HEIGHT1(lds + zerohack, u, smallTrig + zerohack, w, 1, me);
  fft_HEIGHT1(lds + zerohack, v, smallTrig + zerohack, w, 1, me);

  // Compute trig values from scratch.  Good on GPUs with high DP throughput.
#if TAIL_TRIGS == 2
  T2 trig = slowTrig_N(line1 + me * H, ND / NH);

  // Do a little bit of memory access and a little bit of DP math.  Good on a Radeon VII.
#elif TAIL_TRIGS == 1
  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*5;
  // Read a hopefully cached line of data and one non-cached T2 per line
  T2 trig = TFLOAD(&smallTrig[height_trigs + me]);                    // Trig values for line zero, should be cached
  T2 mult = TSLOAD(&smallTrig[height_trigs + G_H + line1]);           // Line multiplier
  trig = cmulFancy(trig, mult);

  // On consumer-grade GPUs, it is likely beneficial to read all trig values.
#else
  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*5;
  // Read pre-computed trig values
  T2 trig = TOLOAD(&smallTrig[height_trigs + line1*G_H + me]);
#endif

#if SINGLE_KERNEL
  if (line1 == 0) {
    // Line 0 is special: it pairs with itself, offseted by 1.
    reverse(lds, u + NH/2, true);
    pairSq(NH/2, u,   u + NH/2, trig, true);
    reverse(lds, u + NH/2, true);

    // Line H/2 also pairs with itself (but without offset).
    T2 trig2 = cmulFancy(trig, TAILT);
    reverse(lds, v + NH/2, false);
    pairSq(NH/2, v,   v + NH/2, trig2, false);
    reverse(lds, v + NH/2, false);
  }
  else {
#else
  if (1) {
#endif
    reverseLine(lds, v);
    pairSq(NH, u, v, trig, false);
    reverseLine(lds, v);
  }

  dependentLaunch();       // Next kernel will be fftMiddleOutFP64 which must dependentLaunchWait before reading data

  fft_HEIGHT2(lds, v, smallTrig, w, 1, me);
  fft_HEIGHT2(lds, u, smallTrig, w, 1, me);

  writeTailFusedLine(v, out, memline2, me);
  writeTailFusedLine(u, out, memline1, me);
}


//
// Create a kernel that uses a double-wide workgroup (u in half the workgroup, v in the other half)
// We hope to get better occupancy with the reduced register usage
//

#else

// Special pairSq for double-wide line 0
void OVERLOAD pairSq2_special(T2 *u, T2 base_squared) {
  u32 me = get_local_id(0);
  for (i32 i = 0; i < NH / 4; ++i, base_squared = mul_t8(base_squared)) {
    if (i == 0 && me == 0) {
      u[0] = SWAP_XY(2 * foo(u[0]));
      u[NH/2] = SWAP_XY(4 * csq(u[NH/2]));
    } else {
      onePairSq(&u[i], &u[NH/2+i], base_squared);
    }
    T2 new_base_squared = mul_t4(base_squared);
    onePairSq(&u[i+NH/4], &u[NH/2+i+NH/4], new_base_squared);
  }
}

KERNEL(G_H * 2) tailSquare(P(T2) out, CP(T2) in, u32 base, Trig smallTrig) {
  local T2 lds[2 * LDS_BYTES / sizeof(T2)];
  const u32 H = ND / SMALL_HEIGHT;

  T2 u[NH];

  u32 line_u = get_line_number(base);
  u32 line_v = line_u ? H - line_u : (H / 2);
  u32 me = get_local_id(0);
  u32 lowMe = me % G_H;  // lane-id in one of the two halves (half-workgroups).

  // We're going to call the halves "first-half" and "second-half".
  bool isSecondHalf = me >= G_H;

  u32 line = !isSecondHalf ? line_u : line_v;

  dependentLaunchWait();   // Previous kernel was fftMiddleInFP64 that launched dependents before writing FP64 data

  // Read lines u and v
  readTailFusedLine(in, u, line, lowMe);

#if FFT_VARIANT_H != 0
  T2 w;
#elif NH == 8
  T2 w = fancyTrig_N(H * lowMe);
#else
  T2 w = slowTrig_N(H * lowMe, ND / NH);
#endif

  u32 zerohack = ZEROHACK_H * (u32) get_group_id(0) / 131072;
  fft_HEIGHT1(lds + zerohack, u, smallTrig + zerohack, w, 2, lowMe);

  // Compute trig values from scratch.  Good on GPUs with high DP throughput.
#if TAIL_TRIGS == 2
  T2 trig = slowTrig_N(line + H * lowMe, ND / NH * 2);

  // Do a little bit of memory access and a little bit of DP math.  Good on a Radeon VII.
#elif TAIL_TRIGS == 1
  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*5;
  // Read a hopefully cached line of data and one non-cached T2 per line
  T2 trig = TFLOAD(&smallTrig[height_trigs + lowMe]);                                 // Trig values for line zero, should be cached
  T2 mult = TSLOAD(&smallTrig[height_trigs + G_H + line_u*2 + isSecondHalf]);         // Two multipliers.  One for line u, one for line v.
  trig = cmulFancy(trig, mult);

  // On consumer-grade GPUs, it is likely beneficial to read all trig values.
#else
  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*5;
  // Read pre-computed trig values
  T2 trig = TOLOAD(&smallTrig[height_trigs + line_u*G_H*2 + me]);
#endif

#if SINGLE_KERNEL
  // Line 0 and H/2 are special: they pair with themselves, line 0 is offseted by 1.
  if (line_u == 0) {
    reverse2(lds, u);
    pairSq2_special(u, trig);
    reverse2(lds, u);
  }
  else {
#else
  if (1) {
#endif
    revCrossLine(lds, u);
    pairSq(NH/2, u, u + NH/2, trig, false);
    revCrossLine(lds, u);
  }

  dependentLaunch();       // Next kernel will be fftMiddleOutFP64 which must dependentLaunchWait before reading data

  fft_HEIGHT2(lds, u, smallTrig, w, 2, lowMe);

  // Write lines u and v
  writeTailFusedLine(u, out, transPos(line, MIDDLE, WIDTH), lowMe);
}

#endif

#endif


/**************************************************************************/
/*            Similar to above, but for an FFT based on FP32              */
/**************************************************************************/

#if FFT_FP32

// Handle the final squaring step on a pair of complex numbers.  Swap real and imaginary results for the inverse FFT.
// We used to conjugate the results, but swapping real and imaginary can save some negations in carry propagation.
void OVERLOAD onePairSq(F2* pa, F2* pb, F2 t_squared) {
  F2 a = *pa;
  F2 b = *pb;

  X2conjb(a, b);
  F2 twoab = mul2(cmul(a, b));                          // 2ab
  *pa = csqa(a, cfma(csq(b), -t_squared, twoab));       // final a = a^2 + 2ab - (bt)^2
  (*pb).x = fma(-2.0f, twoab.x, (*pa).x);               // final b = a^2 - 2ab - (bt)^2
  (*pb).y = fma(2.0f, twoab.y, -(*pa).y);               // conjugate(final b)
  *pa = SWAP_XY(*pa), *pb = SWAP_XY(*pb);
}

void OVERLOAD pairSq(u32 N, F2 *u, F2 *v, F2 base_squared, bool special) {
  u32 me = get_local_id(0);

  for (i32 i = 0; i < NH / 4; ++i, base_squared = mul_t8(base_squared)) {
    if (special && i == 0 && me == 0) {
      u[i] = SWAP_XY(2 * foo(u[i]));
      v[i] = SWAP_XY(4 * csq(v[i]));
    } else {
      onePairSq(&u[i], &v[i], base_squared);
    }

    if (N == NH) {
      onePairSq(&u[i+NH/2], &v[i+NH/2], -base_squared);
    }

    F2 new_base_squared = mul_t4(base_squared);
    onePairSq(&u[i+NH/4], &v[i+NH/4], new_base_squared);

    if (N == NH) {
      onePairSq(&u[i+3*NH/4], &v[i+3*NH/4], -new_base_squared);
    }
  }
}

#if !SINGLE_KERNEL
// The kernel tailSquareZero handles the special cases in tailSquare, i.e. the lines 0 and H/2
// This kernel is launched with 2 workgroups (handling line 0, resp. H/2)
KERNEL(G_H) tailSquareZero(P(T2) out, CP(T2) in, Trig smallTrig) {
  local F2 lds[LDS_BYTES / sizeof(F2)];
  F2 u[NH];
  const u32 H = ND / SMALL_HEIGHT;

  CP(F2) inF2 = (CP(F2)) in;
  P(F2) outF2 = (P(F2)) out;
  TrigFP32 smallTrigF2 = (TrigFP32) smallTrig;

  // This kernel in executed in two workgroups.
  u32 which = get_group_id(0);
  assert(which < 2);

  u32 line = which ? (H/2) : 0;
  u32 me = get_local_id(0);

  dependentLaunch();       // Next kernel will be tailSquareFP32 which must dependentLaunchWait before reading data from fftMiddleInFP32
  dependentLaunchWait();   // Previous kernel was fftMiddleInFP32 that launched dependents before writing FP32 data

  readTailFusedLine(inF2, u, line, me);

  F2 trig = slowTrig_N(line + me * H, ND / NH);

  fft_HEIGHT1(lds, u, smallTrigF2, 1, me);
  reverse(lds, u + NH/2, !which);
  pairSq(NH/2, u,   u + NH/2, trig, !which);
  reverse(lds, u + NH/2, !which);

  fft_HEIGHT1(lds, u, smallTrigF2, 1, me);
  writeTailFusedLine(u, outF2, transPos(line, MIDDLE, WIDTH), me);
}
#endif

#if SINGLE_WIDE

KERNEL(G_H) tailSquare(P(T2) out, CP(T2) in, u32 base, Trig smallTrig) {
  local F2 lds[LDS_BYTES / sizeof(F2)];
  const u32 H = ND / SMALL_HEIGHT;

  CP(F2) inF2 = (CP(F2)) in;
  P(F2) outF2 = (P(F2)) out;
  TrigFP32 smallTrigF2 = (TrigFP32) smallTrig;

  F2 u[NH], v[NH];

  u32 line1 = get_line_number(base);
  u32 line2 = line1 ? H - line1 : (H / 2);
  u32 memline1 = transPos(line1, MIDDLE, WIDTH);
  u32 memline2 = transPos(line2, MIDDLE, WIDTH);

  u32 me = get_local_id(0);

  dependentLaunchWait();   // Previous kernel was fftMiddleInFP32 that launched dependents before writing FP32 data

  readTailFusedLine(inF2, u, line1, me);
  readTailFusedLine(inF2, v, line2, me);

  u32 zerohack = ZEROHACK_H * (u32) get_group_id(0) / 131072;
  fft_HEIGHT1(lds + zerohack, u, smallTrigF2 + zerohack, 1, me);
  fft_HEIGHT1(lds + zerohack, v, smallTrigF2 + zerohack, 1, me);

  // Compute trig values from scratch.  Good on GPUs with high FP throughput.
#if TAIL_TRIGS32 == 2
  F2 trig = slowTrig_N(line1 + me * H, ND / NH);

  // Do a little bit of memory access and a little bit of FP math.
#elif TAIL_TRIGS32 == 1
  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*5;
  // Read a hopefully cached line of data and one non-cached F2 per line
  F2 trig = TFLOAD(&smallTrigF2[height_trigs + me]);                    // Trig values for line zero, should be cached
  F2 mult = TSLOAD(&smallTrigF2[height_trigs + G_H + line1]);           // Line multiplier
  trig = cmulFancy(trig, mult);

  // On consumer-grade GPUs, it is likely beneficial to read all trig values.
#else
  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*5;
  // Read pre-computed trig values
  F2 trig = TOLOAD(&smallTrigF2[height_trigs + line1*G_H + me]);
#endif

#if SINGLE_KERNEL
  if (line1 == 0) {
    // Line 0 is special: it pairs with itself, offseted by 1.
    reverse(lds, u + NH/2, true);
    pairSq(NH/2, u,   u + NH/2, trig, true);
    reverse(lds, u + NH/2, true);

    // Line H/2 also pairs with itself (but without offset).
    F2 trig2 = cmulFancy(trig, TAILT);
    reverse(lds, v + NH/2, false);
    pairSq(NH/2, v,   v + NH/2, trig2, false);
    reverse(lds, v + NH/2, false);
  }
  else {
#else
  if (1) {
#endif
    reverseLine(lds, v);
    pairSq(NH, u, v, trig, false);
    reverseLine(lds, v);
  }

  dependentLaunch();       // Next kernel will be fftMiddleOutFP32 which must dependentLaunchWait before reading data

  fft_HEIGHT2(lds, v, smallTrigF2, 1, me);
  fft_HEIGHT2(lds, u, smallTrigF2, 1, me);

  writeTailFusedLine(v, outF2, memline2, me);
  writeTailFusedLine(u, outF2, memline1, me);
}


//
// Create a kernel that uses a double-wide workgroup (u in half the workgroup, v in the other half)
// We hope to get better occupancy with the reduced register usage
//

#else

// Special pairSq for double-wide line 0
void OVERLOAD pairSq2_special(F2 *u, F2 base_squared) {
  u32 me = get_local_id(0);
  for (i32 i = 0; i < NH / 4; ++i, base_squared = mul_t8(base_squared)) {
    if (i == 0 && me == 0) {
      u[0] = SWAP_XY(2 * foo(u[0]));
      u[NH/2] = SWAP_XY(4 * csq(u[NH/2]));
    } else {
      onePairSq(&u[i], &u[NH/2+i], base_squared);
    }
    F2 new_base_squared = mul_t4(base_squared);
    onePairSq(&u[i+NH/4], &u[NH/2+i+NH/4], new_base_squared);
  }
}

KERNEL(G_H * 2) tailSquare(P(T2) out, CP(T2) in, u32 base, Trig smallTrig) {
  local F2 lds[2 * LDS_BYTES / sizeof(F2)];
  const u32 H = ND / SMALL_HEIGHT;

  CP(F2) inF2 = (CP(F2)) in;
  P(F2) outF2 = (P(F2)) out;
  TrigFP32 smallTrigF2 = (TrigFP32) smallTrig;

  F2 u[NH];

  u32 line_u = get_line_number(base);
  u32 line_v = line_u ? H - line_u : (H / 2);
  u32 me = get_local_id(0);
  u32 lowMe = me % G_H;  // lane-id in one of the two halves (half-workgroups).

  // We're going to call the halves "first-half" and "second-half".
  bool isSecondHalf = me >= G_H;

  u32 line = !isSecondHalf ? line_u : line_v;

  dependentLaunchWait();   // Previous kernel was fftMiddleInFP32 that launched dependents before writing FP32 data

  // Read lines u and v
  readTailFusedLine(inF2, u, line, lowMe);

  u32 zerohack = ZEROHACK_H * (u32) get_group_id(0) / 131072;
  fft_HEIGHT1(lds + zerohack, u, smallTrigF2 + zerohack, 2, lowMe);

  // Compute trig values from scratch.  Good on GPUs with high FP throughput.
#if TAIL_TRIGS32 == 2
  F2 trig = slowTrig_N(line + H * lowMe, ND / NH * 2);

  // Do a little bit of memory access and a little bit of FP math.
#elif TAIL_TRIGS32 == 1
  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*5;
  // Read a hopefully cached line of data and one non-cached F2 per line
  F2 trig = TFLOAD(&smallTrigF2[height_trigs + lowMe]);                                 // Trig values for line zero, should be cached
  F2 mult = TSLOAD(&smallTrigF2[height_trigs + G_H + line_u*2 + isSecondHalf]);         // Two multipliers.  One for line u, one for line v.
  trig = cmulFancy(trig, mult);

  // On consumer-grade GPUs, it is likely beneficial to read all trig values.
#else
  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*5;
  // Read pre-computed trig values
  F2 trig = TOLOAD(&smallTrigF2[height_trigs + line_u*G_H*2 + me]);
#endif

#if SINGLE_KERNEL
  // Line 0 and H/2 are special: they pair with themselves, line 0 is offseted by 1.
  if (line_u == 0) {
    reverse2(lds, u);
    pairSq2_special(u, trig);
    reverse2(lds, u);
  }
  else {
#else
  if (1) {
#endif
    revCrossLine(lds, u);
    pairSq(NH/2, u, u + NH/2, trig, false);
    revCrossLine(lds, u);
  }

  dependentLaunch();       // Next kernel will be fftMiddleOutFP32 which must dependentLaunchWait before reading data

  fft_HEIGHT2(lds, u, smallTrigF2, 2, lowMe);

  // Write lines u and v
  writeTailFusedLine(u, outF2, transPos(line, MIDDLE, WIDTH), lowMe);
}

#endif

#endif


/**************************************************************************/
/*          Similar to above, but for an NTT based on GF(M31^2)           */
/**************************************************************************/

#if NTT_GF31

void OVERLOAD onePairSq(GF31* pa, GF31* pb, GF31 t_squared, const u32 t_squared_type) {
  GF31 a = *pa, b = *pb;
  GF31 b2t2, c, d;

  X2conjb(a, b);
  b2t2 = cmul(csq(b), t_squared);     // b2t2 = b^2 * t_squared
  if (t_squared_type == 0)            // mul t_squared by 1
    c = csq_sub(a, b2t2);             // a^2 - (b^2 * t_squared)
  if (t_squared_type == 1)            // mul t_squared by i
    c = csq_subi(a, b2t2);            // a^2 - i*(b^2 * t_squared)
  if (t_squared_type == 2)            // mul t_squared by -1
    c = csq_add(a, b2t2);             // a^2 - -1*(b^2 * t_squared)
  if (t_squared_type == 3)            // mul t_squared by -i
    c = csq_addi(a, b2t2);            // a^2 - -i*(b^2 * t_squared)
  d = mul2(cmul(a, b));
  X2_conjb(c, d);
  *pa = SWAP_XY(c), *pb = SWAP_XY(d);
}

void OVERLOAD pairSq(u32 N, GF31 *u, GF31 *v, GF31 base_squared, bool special) {
  u32 me = get_local_id(0);

  for (i32 i = 0; i < NH / 4; ++i, base_squared = mul_t8(base_squared)) {
    if (special && i == 0 && me == 0) {
      u[i] = SWAP_XY(mul2(foo(u[i])));
      v[i] = SWAP_XY(shl(csq(v[i]), 2));
    } else {
      onePairSq(&u[i], &v[i], base_squared, 0);
    }

    if (N == NH) {
      onePairSq(&u[i+NH/2], &v[i+NH/2], base_squared, 2);
    }

    onePairSq(&u[i+NH/4], &v[i+NH/4], base_squared, 1);

    if (N == NH) {
      onePairSq(&u[i+3*NH/4], &v[i+3*NH/4], base_squared, 3);
    }
  }
}

#if !SINGLE_KERNEL
// The kernel tailSquareZero handles the special cases in tailSquare, i.e. the lines 0 and H/2
// This kernel is launched with 2 workgroups (handling line 0, resp. H/2)
KERNEL(G_H) tailSquareZeroGF31(P(T2) out, CP(T2) in, Trig smallTrig) {
  local GF31 lds[LDS_BYTES / sizeof(GF31)];
  const u32 H = ND / SMALL_HEIGHT;

  CP(GF31) in31 = (CP(GF31)) (in + DISTGF31);
  P(GF31) out31 = (P(GF31)) (out + DISTGF31);
  TrigGF31 smallTrig31 = (TrigGF31) (smallTrig + DISTHTRIGGF31);

  GF31 u[NH];

  // This kernel in executed in two workgroups.
  u32 which = get_group_id(0);
  assert(which < 2);

  u32 line = which ? (H/2) : 0;
  u32 me = get_local_id(0);

  dependentLaunch();       // Next kernel will be tailSquareGF31 which must dependentLaunchWait before reading data from fftMiddleInGF31
  dependentLaunchWait();   // Previous kernel was fftMiddleInGF31 that launched dependents before writing GF31 data

  readTailFusedLine(in31, u, line, me);

  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*1;
#if TAIL_TRIGS31 >= 1
  GF31 trig = TFLOAD(&smallTrig31[height_trigs + me]);
#if SINGLE_WIDE
  GF31 mult = TSLOAD(&smallTrig31[height_trigs + G_H + line]);
#else
  GF31 mult = TSLOAD(&smallTrig31[height_trigs + G_H + which]);
#endif
  trig = cmul(trig, mult);
#else
#if SINGLE_WIDE
  GF31 trig = TOLOAD(&smallTrig31[height_trigs + line*G_H + me]);
#else
  GF31 trig = TOLOAD(&smallTrig31[height_trigs + which*G_H + me]);
#endif
#endif

  fft_HEIGHT1(lds, u, smallTrig31, 1, me);
  reverse(lds, u + NH/2, !which);
  pairSq(NH/2, u,   u + NH/2, trig, !which);
  reverse(lds, u + NH/2, !which);

  fft_HEIGHT2(lds, u, smallTrig31, 1, me);
  writeTailFusedLine(u, out31, transPos(line, MIDDLE, WIDTH), me);
}
#endif

#if SINGLE_WIDE

KERNEL(G_H) tailSquareGF31(P(T2) out, CP(T2) in, u32 base, Trig smallTrig) {
  local GF31 lds[LDS_BYTES / sizeof(GF31)];
  const u32 H = ND / SMALL_HEIGHT;

  CP(GF31) in31 = (CP(GF31)) (in + DISTGF31);
  P(GF31) out31 = (P(GF31)) (out + DISTGF31);
  TrigGF31 smallTrig31 = (TrigGF31) (smallTrig + DISTHTRIGGF31);

  GF31 u[NH], v[NH];

  u32 line1 = get_line_number(base);
  u32 line2 = line1 ? H - line1 : (H / 2);
  u32 memline1 = transPos(line1, MIDDLE, WIDTH);
  u32 memline2 = transPos(line2, MIDDLE, WIDTH);

  u32 me = get_local_id(0);

  dependentLaunchWait();   // Previous kernel was fftMiddleInGF31 that launched dependents before writing GF31 data

  readTailFusedLine(in31, u, line1, me);
  readTailFusedLine(in31, v, line2, me);

  u32 zerohack = ZEROHACK_H * (u32) get_group_id(0) / 131072;
  fft_HEIGHT1(lds + zerohack, u, smallTrig31 + zerohack, 1, me);
  fft_HEIGHT1(lds + zerohack, v, smallTrig31 + zerohack, 1, me);

  // Do a little bit of memory access and a little bit of math.
#if TAIL_TRIGS31 >= 1
  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*1;
  // Read a hopefully cached line of data and one non-cached GF31 per line
  GF31 trig = TFLOAD(&smallTrig31[height_trigs + me]);                    // Trig values for line zero, should be cached
  GF31 mult = TSLOAD(&smallTrig31[height_trigs + G_H + line1]);           // Line multiplier
  trig = cmul(trig, mult);

  // On consumer-grade GPUs, it is likely beneficial to read all trig values.
#else
  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*1;
  // Read pre-computed trig values
  GF31 trig = TOLOAD(&smallTrig31[height_trigs + line1*G_H + me]);
#endif

#if SINGLE_KERNEL
  if (line1 == 0) {
    // Line 0 is special: it pairs with itself, offseted by 1.
    reverse(lds, u + NH/2, true);
    pairSq(NH/2, u,   u + NH/2, trig, true);
    reverse(lds, u + NH/2, true);

    // Line H/2 also pairs with itself (but without offset).
    GF31 trig2 = cmul(trig, TAILTGF31);
    reverse(lds, v + NH/2, false);
    pairSq(NH/2, v,   v + NH/2, trig2, false);
    reverse(lds, v + NH/2, false);
  }
  else {
#else
  if (1) {
#endif
    reverseLine(lds, v);
    pairSq(NH, u, v, trig, false);
    reverseLine(lds, v);
  }

  dependentLaunch();       // Next kernel will be fftMiddleOutGF31 which must dependentLaunchWait before reading data

  fft_HEIGHT2(lds, v, smallTrig31, 1, me);
  fft_HEIGHT2(lds, u, smallTrig31, 1, me);

  writeTailFusedLine(v, out31, memline2, me);
  writeTailFusedLine(u, out31, memline1, me);
}


//
// Create a kernel that uses a double-wide workgroup (u in half the workgroup, v in the other half)
// We hope to get better occupancy with the reduced register usage
//

#else

// Special pairSq for double-wide line 0
void OVERLOAD pairSq2_special(GF31 *u, GF31 base_squared) {
  u32 me = get_local_id(0);
  for (i32 i = 0; i < NH / 4; ++i, base_squared = mul_t8(base_squared)) {
    if (i == 0 && me == 0) {
      u[0] = SWAP_XY(mul2(foo(u[0])));
      u[NH/2] = SWAP_XY(shl(csq(u[NH/2]), 2));
    } else {
      onePairSq(&u[i], &u[NH/2+i], base_squared, 0);
    }
    onePairSq(&u[i+NH/4], &u[NH/2+i+NH/4], base_squared, 1);
  }
}

KERNEL(G_H * 2) tailSquareGF31(P(T2) out, CP(T2) in, u32 base, Trig smallTrig) {
  local GF31 lds[2 * LDS_BYTES / sizeof(GF31)];
  const u32 H = ND / SMALL_HEIGHT;

  CP(GF31) in31 = (CP(GF31)) (in + DISTGF31);
  P(GF31) out31 = (P(GF31)) (out + DISTGF31);
  TrigGF31 smallTrig31 = (TrigGF31) (smallTrig + DISTHTRIGGF31);

  GF31 u[NH];

  u32 line_u = get_line_number(base);
#if GOOD_THOMAS3 || GOOD_THOMAS7 || GOOD_THOMAS9
  // Map the grid's Hermitian-pair ordinal to one independent power-of-two
  // spectrum.  The odd Good--Thomas radix is coupled outside this tail.
  u32 const gt_factor = GOOD_THOMAS9 ? 9 : GOOD_THOMAS7 ? 7 : 3;
  u32 const gt_span = (MIDDLE / gt_factor) * WIDTH;
  u32 const gt_pairs = gt_span / 2;
  u32 const gt_channel = line_u / gt_pairs;
  u32 const gt_line = line_u % gt_pairs;
  u32 const gt_base = gt_channel * gt_span;
  line_u = gt_base + gt_line;
  u32 line_v = gt_base + (gt_line ? gt_span - gt_line : gt_pairs);
#else
  u32 const gt_line = line_u;
  u32 line_v = line_u ? H - line_u : (H / 2);
#endif
  u32 me = get_local_id(0);
  u32 lowMe = me % G_H;  // lane-id in one of the two halves (half-workgroups).

  // We're going to call the halves "first-half" and "second-half".
  bool isSecondHalf = me >= G_H;

  u32 line = !isSecondHalf ? line_u : line_v;

  dependentLaunchWait();   // Previous kernel was fftMiddleInGF31 that launched dependents before writing GF31 data

  // Read lines u and v
  readTailFusedLine(in31, u, line, lowMe);

  u32 zerohack = ZEROHACK_H * (u32) get_group_id(0) / 131072;
  fft_HEIGHT1(lds + zerohack, u, smallTrig31 + zerohack, 2, lowMe);

  // Do a little bit of memory access and a little bit of math.  Good on a Radeon VII.
#if TAIL_TRIGS31 >= 1
  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*1;
  // Read a hopefully cached line of data and one non-cached GF31 per line
  GF31 trig = TFLOAD(&smallTrig31[height_trigs + lowMe]);                                 // Trig values for line zero, should be cached
  GF31 mult = TSLOAD(&smallTrig31[height_trigs + G_H + gt_line*2 + isSecondHalf]);         // Two multipliers.  One for line u, one for line v.
  trig = cmul(trig, mult);

  // On consumer-grade GPUs, it is likely beneficial to read all trig values.
#else
  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*1;
  // Read pre-computed trig values
  GF31 trig = TOLOAD(&smallTrig31[height_trigs + gt_line*G_H*2 + me]);
#endif

#if SINGLE_KERNEL
  // Line 0 and H/2 are special: they pair with themselves, line 0 is offseted by 1.
  if (gt_line == 0) {
    reverse2(lds, u);
    pairSq2_special(u, trig);
    reverse2(lds, u);
  }
  else {
#else
  if (1) {
#endif
    revCrossLine(lds, u);
    pairSq(NH/2, u, u + NH/2, trig, false);
    revCrossLine(lds, u);
  }

  dependentLaunch();       // Next kernel will be fftMiddleOutGF31 which must dependentLaunchWait before reading data

  fft_HEIGHT2(lds, u, smallTrig31, 2, lowMe);

  // Write lines u and v
  writeTailFusedLine(u, out31, transPos(line, MIDDLE, WIDTH), lowMe);
}

#endif

#endif


/**************************************************************************/
/*          Similar to above, but for an NTT based on GF(M61^2)           */
/**************************************************************************/

#if NTT_GF61

#if GOLD_PAIR

// A GF61 value carries the two independent length-N/2 transforms of the even
// and odd words of one length-N scalar Goldilocks transform.  At frequency k,
// the missing radix-2 coupling followed by a pointwise square is
//
//   E' = E^2 + root_N/2^k O^2
//   O' = 2 E O.
//
// The existing tail trig is precisely root_N/2^k.  The four register quarters
// advance by 1, i, -1, and -i, as in pairSq below, but no Hermitian partner or
// data conjugation is involved.
GF61 goldPairSquareOne(GF61 a, Z61 root) {
  Z61 const even2 = mul(a.x, a.x);
  Z61 const odd2 = mul(a.y, a.y);
  return U2(add(even2, mul(root, odd2)), mul2(mul(a.x, a.y)));
}

void goldPairSquareLine(GF61 *u, GF61 trig) {
  Z61 root = trig.x;
  for (u32 i = 0; i < NH / 4; ++i, root = mul(root, GOLD_T8)) {
    u[i] = goldPairSquareOne(u[i], root);
    u[i + NH / 4] = goldPairSquareOne(u[i + NH / 4],
                                      mul(root, GOLD_I));
    u[i + NH / 2] = goldPairSquareOne(u[i + NH / 2], neg(root));
    u[i + 3 * NH / 4] = goldPairSquareOne(
      u[i + 3 * NH / 4], neg(mul(root, GOLD_I)));
  }
}

// The NTT kernels use the same (forward-root) transform for both directions.
// The original quadratic-field tail obtains the inverse by reversing the
// spectrum as part of pairSq.  Scalar even/odd planes need that permutation
// explicitly.  The double-wide tail already co-locates line k with line -k;
// reverse each height coordinate and cross the two LDS partitions, except for
// the self-paired lines 0 and H/2.
void goldPairReverseSpectrum(local GF61 *lds61, GF61 *u, u32 line, u32 lineCount) {
  u32 const me = get_local_id(0);
  u32 const lowMe = me % G_H;
  u32 const half = me / G_H;
  u32 const sourceHalf = half;
  bool const crossLines = line != 0 && line != lineCount / 2;
  u32 const destinationHalf = crossLines ? 1 - half : half;
  u32 const stride = LDS_BYTES / sizeof(Z61);
  local Z61 *lds = (local Z61 *)lds61;

  bar();
  for (u32 i = 0; i < NH; ++i) {
    u32 const source = i * G_H + lowMe;
    u32 const destination = line == 0 && source == 0 ? 0 :
                            SMALL_HEIGHT - source - (line == 0 ? 0 : 1);
    lds[destinationHalf * stride + destination] = u[i].x;
  }
  bar();
  for (u32 i = 0; i < NH; ++i) {
    u[i].x = lds[sourceHalf * stride + i * G_H + lowMe];
  }

  bar();
  for (u32 i = 0; i < NH; ++i) {
    u32 const source = i * G_H + lowMe;
    u32 const destination = line == 0 && source == 0 ? 0 :
                            SMALL_HEIGHT - source - (line == 0 ? 0 : 1);
    lds[destinationHalf * stride + destination] = u[i].y;
  }
  bar();
  for (u32 i = 0; i < NH; ++i) {
    u[i].y = lds[sourceHalf * stride + i * G_H + lowMe];
  }
}

#if SINGLE_WIDE
#error GOLD_PAIR currently requires the double-wide tail (TAIL_KERNELS=2 or 3)
#endif

#endif

void OVERLOAD onePairSq(GF61* pa, GF61* pb, GF61 t_squared, const u32 t_squared_type) {
#if RIESEL_PAIR
  GF61 a = *pa, b = *pb;
  X2conjb(a, b);
  GF61 b2t2 = cmul(csq(b), t_squared);
  GF61 c;
  if (t_squared_type == 0) c = csq_sub(a, b2t2);
  if (t_squared_type == 1) c = csq_subi(a, b2t2);
  if (t_squared_type == 2) c = csq_add(a, b2t2);
  if (t_squared_type == 3) c = csq_addi(a, b2t2);
  GF61 d = mul2(cmul(a, b));
  X2_conjb(c, d);
  *pa = SWAP_XY(c), *pb = SWAP_XY(d);
#else
  GF61 a = *pa, b = *pb;
  GF61 a2, b2, b2t2, ab, addin, c, d;

// This code should be faster (saves at least one wide mul) but the CUDA compiler makes poorer decisions regarding register usage resulting in local memory usage
#if ENABLE_BETTER_ONEPAIRSQ
  X2qconjb(&a, &b);                             // X2(a, conjugate(b)).  a.x range is 0..2+, a.y range is -1-..1+, b.x range is -1-..1+, b.y range is 0..2+
  a.y += 2*M61;                                 // a range is  0..2+ / 1-..3+
  b.x += 2*M61;                                 // b range is 1-..3+ / 0..2+

  ab = addq(a, b);                              // Compute 2ab as (a + b)^2 - a^2 - b^2.  ab range is 1-..5+
  a2 = csqq(a, 3, 4);                           // a2 = a^2, a2 range is 0..2+
  b2 = csq(b, 4, 3);                            // b2 = b^2, b2 range is 0..1+

  addin = neg(addq(a2, b2), 4);                 // add this into the csq of a+b, addin range is 0..4
  d = csqa(ab, addin, 6);                       // d = 2ab, range is 0..1+

  b2t2 = cmul(b2, t_squared);                   // b2t2 = b^2 * t_squared, b2t2 range is 0..1+

  if (t_squared_type == 0) {                    // mul t_squared by 1
    c = subq(a2, b2t2);                         // c range is -1-..2+
    X2q_conjb(&c, &d);                          // X2(c, d); d = conjugate(d); c range is -1-..3+, d.x range is -1-..2+, d.y range is -2-..1+
    c = modM61q(c, 2);
    d = modM61q(d, 3);
  }
  if (t_squared_type == 1) {                    // mul t_squared by i
    c = subiq(a2, b2t2);                        // c.x range is 0..3+, c.y range is -1-..2+
    X2q_conjb(&c, &d);                          // X2(c, d); d = conjugate(d); c.x range is 0..4+, c.y range is -1..3+, d.x range is -1-..3+, d.y range is -2-..1+
    c = modM61q(c, 0, 2);
    d = modM61q(d, 2, 3);
  }
  if (t_squared_type == 2) {                    // mul t_squared by -1
    c = addq(a2, b2t2);                         // c range is 0..3+
    X2q_conjb(&c, &d);                          // X2(c, d); d = conjugate(d); c range is 0..4+, d.x range is -1-..3+, d.y range is -3-..1+
    c = modM61q(c, 0);
    d = modM61q(d, 2, 4);
  }
  if (t_squared_type == 3) {                    // mul t_squared by -i
    c = addiq(a2, b2t2);                        // c.x range is -1-..2+, c.y range is 0..3+
    X2q_conjb(&c, &d);                          // X2(c, d); d = conjugate(d); c.x range is -1-..3+, c.y range is 0..4+, d.x range is -1-..2+, d.y range is -3-..1+
    c = modM61q(c, 2, 0);
    d = modM61q(d, 2, 4);
  }
#else
  X2conjb(a, b);                                // X2(a, conjugate(b))
  a2 = csqq(a, 2);                              // a2 = a^2, a2.x range is 0..7+, a2.y range is 0..2+
  a2.x = modM61(a2.x);                          // a2.x range is 0..1+, a2.y range is 0..2+
  b2t2 = cmul(csq(b), t_squared);               // b2t2 = b^2 * t_squared, b2t2 range is 0..1+
  d = cmul(a, b); d = d + d;                    // d = 2ab, d range is 0..2+
  if (t_squared_type == 0) {                    // mul t_squared by 1
    c = subq(a2, b2t2);                         // c.x range is -1..2+, c.y range is -1-..3+
    X2q_conjb(&c, &d);                          // X2(c, d); d = conjugate(d); c.x range is -1-..4+, c.y range is -1-..5+, d.x range is -3-..2+, d.y range is -3-..3+
    c = modM61q(c, 2);
    d = modM61q(d, 4);
  }
  if (t_squared_type == 1) {                    // mul t_squared by i
    c = subiq(a2, b2t2);                        // c.x range is 0..3+, c.y range is -1-..3+
    X2q_conjb(&c, &d);                          // X2(c, d); d = conjugate(d); c.x range is 0..5+, c.y range is -1..5+, d.x range is -2-..3+, d.y range is -3-..3+
    c = modM61q(c, 0, 2);
    d = modM61q(d, 4);
  }
  if (t_squared_type == 2) {                    // mul t_squared by -1
    c = addq(a2, b2t2);                         // c.x range is 0..3+, c.y range is 0..4+
    X2q_conjb(&c, &d);                          // X2(c, d); d = conjugate(d); c.x range is 0..5+, c.y range is 0..6+, d.x range is -2-..3+, d.y range is -4-..2+
    c = modM61q(c, 0);
    d = modM61q(d, 3, 5);
  }
  if (t_squared_type == 3) {                    // mul t_squared by -i
    c = addiq(a2, b2t2);                        // c.x range is -1-..2+, c.y range is 0..4+
    X2q_conjb(&c, &d);                          // X2(c, d); d = conjugate(d); c.x range is -1-..4+, c.y range is 0..6+, d.x range is -3-..2+, d.y range is -4-..2+
    c = modM61q(c, 2, 0);
    d = modM61q(d, 4, 5);
  }
#endif
  *pa = SWAP_XY(c), *pb = SWAP_XY(d);
#endif
}

void OVERLOAD pairSq(u32 N, GF61 *u, GF61 *v, GF61 base_squared, bool special) {
  u32 me = get_local_id(0);

  for (i32 i = 0; i < NH / 4; ++i, base_squared = mul_t8(base_squared)) {
    if (special && i == 0 && me == 0) {
      u[i] = SWAP_XY(mul2(foo(u[i])));
      v[i] = SWAP_XY(shl(csq(v[i]), 2));
    } else {
      onePairSq(&u[i], &v[i], base_squared, 0);
    }

    if (N == NH) {
      onePairSq(&u[i+NH/2], &v[i+NH/2], base_squared, 2);
    }

    onePairSq(&u[i+NH/4], &v[i+NH/4], base_squared, 1);

    if (N == NH) {
      onePairSq(&u[i+3*NH/4], &v[i+3*NH/4], base_squared, 3);
    }
  }
}

#if !SINGLE_KERNEL
// The kernel tailSquareZero handles the special cases in tailSquare, i.e. the lines 0 and H/2
// This kernel is launched with 2 workgroups (handling line 0, resp. H/2)
KERNEL(G_H) tailSquareZeroGF61(P(T2) out, CP(T2) in, Trig smallTrig) {
  local GF61 lds[LDS_BYTES / sizeof(GF61)];
  const u32 H = ND / SMALL_HEIGHT;

  CP(GF61) in61 = (CP(GF61)) (in + DISTGF61);
  P(GF61) out61 = (P(GF61)) (out + DISTGF61);
  TrigGF61 smallTrig61 = (TrigGF61) (smallTrig + DISTHTRIGGF61);

  GF61 u[NH];

  // This kernel in executed in two workgroups.
  u32 which = get_group_id(0);
  assert(which < 2);

  u32 line = which ? (H/2) : 0;
  u32 me = get_local_id(0);

  dependentLaunch();       // Next kernel will be tailSquareGF61 which must dependentLaunchWait before reading data from fftMiddleInGF61
  dependentLaunchWait();   // Previous kernel was fftMiddleInGF61 that launched dependents before writing GF61 data

  readTailFusedLine(in61, u, line, me);

  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*1;
#if TAIL_TRIGS61 >= 1
  GF61 trig = TFLOAD(&smallTrig61[height_trigs + me]);
#if SINGLE_WIDE
  GF61 mult = TSLOAD(&smallTrig61[height_trigs + G_H + line]);
#else
  GF61 mult = TSLOAD(&smallTrig61[height_trigs + G_H + which]);
#endif
  #if GOLD_PAIR
  trig = cmulTrig(trig, mult);
  #else
  trig = cmul(trig, mult);
  #endif
#else
#if SINGLE_WIDE
  GF61 trig = TOLOAD(&smallTrig61[height_trigs + line*G_H + me]);
#else
  GF61 trig = TOLOAD(&smallTrig61[height_trigs + which*G_H + me]);
#endif
#endif

  fft_HEIGHT1(lds, u, smallTrig61, 1, me);
#if GOLD_PAIR
  goldPairSquareLine(u, trig);
#else
  reverse(lds, u + NH/2, !which);
  pairSq(NH/2, u,   u + NH/2, trig, !which);
  reverse(lds, u + NH/2, !which);
#endif

  fft_HEIGHT2(lds, u, smallTrig61, 1, me);
  writeTailFusedLine(u, out61, transPos(line, MIDDLE, WIDTH), me);
}
#endif

#if SINGLE_WIDE

KERNEL(G_H) tailSquareGF61(P(T2) out, CP(T2) in, u32 base, Trig smallTrig) {
  local GF61 lds[LDS_BYTES / sizeof(GF61)];
  const u32 H = ND / SMALL_HEIGHT;

  CP(GF61) in61 = (CP(GF61)) (in + DISTGF61);
  P(GF61) out61 = (P(GF61)) (out + DISTGF61);
  TrigGF61 smallTrig61 = (TrigGF61) (smallTrig + DISTHTRIGGF61);

  GF61 u[NH], v[NH];

  u32 line1 = get_line_number(base);
  u32 line2 = line1 ? H - line1 : (H / 2);
  u32 memline1 = transPos(line1, MIDDLE, WIDTH);
  u32 memline2 = transPos(line2, MIDDLE, WIDTH);

  u32 me = get_local_id(0);

  dependentLaunchWait();   // Previous kernel was fftMiddleInGF61 that launched dependents before writing GF61 data

  readTailFusedLine(in61, u, line1, me);
  readTailFusedLine(in61, v, line2, me);

  u32 zerohack = ZEROHACK_H * (u32) get_group_id(0) / 131072;
  fft_HEIGHT1(lds + zerohack, u, smallTrig61 + zerohack, 1, me);
  fft_HEIGHT1(lds + zerohack, v, smallTrig61 + zerohack, 1, me);

  // Do a little bit of memory access and a little bit of math.
#if TAIL_TRIGS61 >= 1
  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*1;
  // Read a hopefully cached line of data and one non-cached GF61 per line
  GF61 trig = TFLOAD(&smallTrig61[height_trigs + me]);                    // Trig values for line zero, should be cached
  GF61 mult = TSLOAD(&smallTrig61[height_trigs + G_H + line1]);           // Line multiplier
  #if GOLD_PAIR
  trig = cmulTrig(trig, mult);
  #else
  trig = cmul(trig, mult);
  #endif

  // On consumer-grade GPUs, it is likely beneficial to read all trig values.
#else
  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*1;
  // Read pre-computed trig values
  GF61 trig = TOLOAD(&smallTrig61[height_trigs + line1*G_H + me]);
#endif

#if SINGLE_KERNEL
  if (line1 == 0) {
    // Line 0 is special: it pairs with itself, offseted by 1.
    reverse(lds, u + NH/2, true);
    pairSq(NH/2, u,   u + NH/2, trig, true);
    reverse(lds, u + NH/2, true);

    // Line H/2 also pairs with itself (but without offset).
    GF61 trig2 = cmul(trig, TAILTGF61);
    reverse(lds, v + NH/2, false);
    pairSq(NH/2, v,   v + NH/2, trig2, false);
    reverse(lds, v + NH/2, false);
  }
  else {
#else
  if (1) {
#endif
    reverseLine(lds, v);
    pairSq(NH, u, v, trig, false);
    reverseLine(lds, v);
  }

  dependentLaunch();       // Next kernel will be fftMiddleOutGF61 which must dependentLaunchWait before reading data

  fft_HEIGHT2(lds, v, smallTrig61, 1, me);
  fft_HEIGHT2(lds, u, smallTrig61, 1, me);

  writeTailFusedLine(v, out61, memline2, me);
  writeTailFusedLine(u, out61, memline1, me);
}


//
// Create a kernel that uses a double-wide workgroup (u in half the workgroup, v in the other half)
// We hope to get better occupancy with the reduced register usage
//

#else

// Special pairSq for double-wide line 0
void OVERLOAD pairSq2_special(GF61 *u, GF61 base_squared) {
  u32 me = get_local_id(0);
  for (i32 i = 0; i < NH / 4; ++i, base_squared = mul_t8(base_squared)) {
    if (i == 0 && me == 0) {
      u[0] = SWAP_XY(mul2(foo(u[0])));
      u[NH/2] = SWAP_XY(shl(csq(u[NH/2]), 2));
    } else {
      onePairSq(&u[i], &u[NH/2+i], base_squared, 0);
    }
    onePairSq(&u[i+NH/4], &u[NH/2+i+NH/4], base_squared, 1);
  }
}

KERNEL(G_H * 2) tailSquareGF61(P(T2) out, CP(T2) in, u32 base, Trig smallTrig) {
  local GF61 lds[2 * LDS_BYTES / sizeof(GF61)];
  const u32 H = ND / SMALL_HEIGHT;

  CP(GF61) in61 = (CP(GF61)) (in + DISTGF61);
  P(GF61) out61 = (P(GF61)) (out + DISTGF61);
  TrigGF61 smallTrig61 = (TrigGF61) (smallTrig + DISTHTRIGGF61);

  GF61 u[NH];

  u32 line_u = get_line_number(base);
#if GOOD_THOMAS3 || GOOD_THOMAS7 || GOOD_THOMAS9
  u32 const gt_factor = GOOD_THOMAS9 ? 9 : GOOD_THOMAS7 ? 7 : 3;
  u32 const gt_span = (MIDDLE / gt_factor) * WIDTH;
  u32 const gt_pairs = gt_span / 2;
  u32 const gt_channel = line_u / gt_pairs;
  u32 const gt_line = line_u % gt_pairs;
  u32 const gt_base = gt_channel * gt_span;
  line_u = gt_base + gt_line;
  u32 line_v = gt_base + (gt_line ? gt_span - gt_line : gt_pairs);
#else
  u32 const gt_line = line_u;
  u32 line_v = line_u ? H - line_u : (H / 2);
#endif
  u32 me = get_local_id(0);
  u32 lowMe = me % G_H;  // lane-id in one of the two halves (half-workgroups).

  // We're going to call the halves "first-half" and "second-half".
  bool isSecondHalf = me >= G_H;

  u32 line = !isSecondHalf ? line_u : line_v;

  dependentLaunchWait();   // Previous kernel was fftMiddleInGF61 that launched dependents before writing GF61 data

  // Read lines u and v
  readTailFusedLine(in61, u, line, lowMe);

  u32 zerohack = ZEROHACK_H * (u32) get_group_id(0) / 131072;
  fft_HEIGHT1(lds + zerohack, u, smallTrig61 + zerohack, 2, lowMe);

  // Do a little bit of memory access and a little bit of math.  Good on a Radeon VII.
#if TAIL_TRIGS61 >= 1
  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*1;
  // Read a hopefully cached line of data and one non-cached GF61 per line
  GF61 trig = TFLOAD(&smallTrig61[height_trigs + lowMe]);                                 // Trig values for line zero, should be cached
  GF61 mult = TSLOAD(&smallTrig61[height_trigs + G_H + gt_line*2 + isSecondHalf]);         // Two multipliers.  One for line u, one for line v.
  trig = cmul(trig, mult);

  // On consumer-grade GPUs, it is likely beneficial to read all trig values.
#else
  // Calculate number of trig values used by fft_HEIGHT (see genSmallTrigCombo in trigBufCache.cpp)
  // The trig values used here are pre-computed and stored after the fft_HEIGHT trig values.
  u32 height_trigs = SMALL_HEIGHT*1;
  // Read pre-computed trig values
  GF61 trig = TOLOAD(&smallTrig61[height_trigs + gt_line*G_H*2 + me]);
#endif

#if GOLD_PAIR
  goldPairSquareLine(u, trig);
  goldPairReverseSpectrum(lds, u, line, H);
#else
#if SINGLE_KERNEL
  // Line 0 and H/2 are special: they pair with themselves, line 0 is offseted by 1.
  if (gt_line == 0) {
    reverse2(lds, u);
    pairSq2_special(u, trig);
    reverse2(lds, u);
  }
  else {
#else
  if (1) {
#endif
    revCrossLine(lds, u);
    pairSq(NH/2, u, u + NH/2, trig, false);
    revCrossLine(lds, u);
  }
#endif

  dependentLaunch();       // Next kernel will be fftMiddleOutGF61 which must dependentLaunchWait before reading data

  fft_HEIGHT2(lds, u, smallTrig61, 2, lowMe);

  // Write lines u and v
  writeTailFusedLine(u, out61, transPos(line, MIDDLE, WIDTH), lowMe);
}

#endif

#endif
