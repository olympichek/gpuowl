// Copyright (C) Mihai Preda

#include "base.cl"
#include "fftwidth.cl"
#include "middle.cl"

#if FFT_FP64

// Do the ending fft_WIDTH after an fftMiddleOut.  This is the same as the first half of carryFused.
KERNEL(G_W) fftW(P(T2) out, CP(T2) in, Trig smallTrig) {
  local T2 lds[LDS_BYTES / sizeof(T2)];

  T2 u[NW];
  u32 g = get_group_id(0);
  u32 me = get_local_id(0);

  dependentLaunchWait();   // Previous kernel was fftMiddleOut

  readCarryFusedLine(in, u, g, me);
  fft_WIDTH(lds, u, smallTrig, 1, me);
  out += WIDTH * g;
  write(G_W, NW, u, out, 0);
}

#endif


/**************************************************************************/
/*            Similar to above, but for an FFT based on FP32              */
/**************************************************************************/

#if FFT_FP32

// Do the ending fft_WIDTH after an fftMiddleOut.  This is the same as the first half of carryFused.
KERNEL(G_W) fftW(P(T2) out, CP(T2) in, Trig smallTrig) {
  local F2 lds[LDS_BYTES / sizeof(F2)];

  CP(F2) inF2 = (CP(F2)) in;
  P(F2) outF2 = (P(F2)) out;
  TrigFP32 smallTrigF2 = (TrigFP32) smallTrig;

  F2 u[NW];
  u32 g = get_group_id(0);
  u32 me = get_local_id(0);

  dependentLaunchWait();   // Previous kernel was fftMiddleOut

  readCarryFusedLine(inF2, u, g, me);
  fft_WIDTH(lds, u, smallTrigF2, 1, me);
  outF2 += WIDTH * g;
  write(G_W, NW, u, outF2, 0);
}

#endif


/**************************************************************************/
/*          Similar to above, but for an NTT based on GF(M31^2)           */
/**************************************************************************/

#if NTT_GF31

KERNEL(G_W) fftWGF31(P(T2) out, CP(T2) in, Trig smallTrig) {
  local GF31 lds[LDS_BYTES / sizeof(GF31)];

  CP(GF31) in31 = (CP(GF31)) (in + DISTGF31);
  P(GF31) out31 = (P(GF31)) (out + DISTGF31);
  TrigGF31 smallTrig31 = (TrigGF31) (smallTrig + DISTWTRIGGF31);

  GF31 u[NW];
  u32 g = get_group_id(0);
  u32 me = get_local_id(0);

  dependentLaunchWait();   // Previous kernel was fftMiddleOut

  readCarryFusedLine(in31, u, g, me);
  fft_WIDTH(lds, u, smallTrig31, 1, me);
  out31 += WIDTH * g;
  write(G_W, NW, u, out31, 0);
}

#if DETACHED_M31_EDGE

// The forward half of the detached M31 width edge.  The detached carryFused
// writes weighted pre-width GF31 residues flat (one WIDTH-sized block per
// line); this kernel applies the forward width transform and produces the
// standard carry-fused line layout expected by fftMiddleInGF31.
KERNEL(G_W) fftWOut31(P(T2) out, CP(T2) in, Trig smallTrig) {
  local GF31 lds[LDS_BYTES / sizeof(GF31)];

  CP(GF31) in31 = (CP(GF31)) in;    // Flat detach buffer, no field offset
  P(GF31) out31 = (P(GF31)) (out + DISTGF31);
  TrigGF31 smallTrig31 = (TrigGF31) (smallTrig + DISTWTRIGGF31);

  GF31 u[NW];
  u32 g = get_group_id(0);
  u32 me = get_local_id(0);

  read(G_W, NW, u, in31 + WIDTH * g, 0);
  fft_WIDTH2(lds, u, smallTrig31, 1, me);
  writeCarryFusedLine(u, out31, g, me);
}

#endif

#endif


/**************************************************************************/
/*          Similar to above, but for an NTT based on GF(M61^2)           */
/**************************************************************************/

#if NTT_GF61

KERNEL(G_W) fftWGF61(P(T2) out, CP(T2) in, Trig smallTrig) {
  local GF61 lds[LDS_BYTES / sizeof(GF61)];

  CP(GF61) in61 = (CP(GF61)) (in + DISTGF61);
  P(GF61) out61 = (P(GF61)) (out + DISTGF61);
  TrigGF61 smallTrig61 = (TrigGF61) (smallTrig + DISTWTRIGGF61);

  GF61 u[NW];
  u32 g = get_group_id(0);
  u32 me = get_local_id(0);

  dependentLaunchWait();   // Previous kernel was fftMiddleOut

  readCarryFusedLine(in61, u, g, me);
  fft_WIDTH(lds, u, smallTrig61, 1, me);
  out61 += WIDTH * g;
  write(G_W, NW, u, out61, 0);
}

#endif
